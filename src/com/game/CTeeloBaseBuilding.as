///////////////////////////////////////////////////////////
//  CTeeloBaseBuilding.as
//  Macromedia ActionScript Implementation of the Class CTeeloBaseBuilding
//  Generated by Enterprise Architect
//  Created on:      20-Dec-2010 3:35:24 PM
//  Original author: poof!
///////////////////////////////////////////////////////////

package com.game
{
	import com.game.CPlayerTeelos;
	import flash.display.DisplayObjectContainer;
	import flash.events.MouseEvent;
	
	/**
	 * @author poof!
	 * @version 1.0
	 * @created 20-Dec-2010 3:35:24 PM
	 */
	public class CTeeloBaseBuilding extends CPlayerTeelos
	{
	    private var m_build:Boolean;
		
		protected var m_buildProgress: int;
		protected var m_buildMax: int;
		protected var m_buildTick:int;
		
		public function CTeeloBaseBuilding(){

		}
		
		override public function initialize():void 
		{
			super.initialize();
			
			m_sprite.mouseEnabled = m_sprite.mouseChildren = true;
			m_buildMax = 10;
		}
		
		override public function onCreate(lane:int, targetPosX:int, container:DisplayObjectContainer):void 
		{
			super.onCreate(lane, targetPosX, container);
			
			m_buildProgress = 0;
			m_buildTick = 0;
			m_build = false;
		}
		
		override public function onRemove():void 
		{
			if ( !isBuildComplete() )
			{
				unregisterEvents();
			}
			super.onRemove();
		}
		
		private function registerEvents():void
		{
			m_sprite.addEventListener( MouseEvent.MOUSE_DOWN, onMouseDown );
			m_sprite.addEventListener( MouseEvent.MOUSE_UP, onMouseUp );
			m_sprite.addEventListener( MouseEvent.MOUSE_OVER, onMouseOver );
			m_sprite.addEventListener( MouseEvent.MOUSE_OUT, onMouseOut );
		}
		
		private function unregisterEvents():void
		{
			m_sprite.removeEventListener( MouseEvent.MOUSE_DOWN, onMouseDown );
			m_sprite.removeEventListener( MouseEvent.MOUSE_UP, onMouseUp );
			m_sprite.removeEventListener( MouseEvent.MOUSE_OVER, onMouseOver );
			m_sprite.removeEventListener( MouseEvent.MOUSE_OUT, onMouseOut );
		}
		
		override public function update(elapsedTime:int):void 
		{
			super.update(elapsedTime);
			
			if ( m_build && !isBuildComplete() )
			{
				m_buildTick += elapsedTime;
				if ( m_buildTick > 1000 )
				{
					m_buildTick = 0;
					build();
				}
			}
		}
		
		protected function build():void
		{
			m_buildProgress++;
			SoundManager.getInstance().playSFX("build");
		}
		
		protected function onMouseOver( event:MouseEvent ):void
		{
			trace("Mouse Over");
			if ( !isBuildComplete() )
			{
				State_GameLoop.getInstance().showBuildCursor(true);
			}
		}
		
		protected function onMouseOut( event:MouseEvent ):void
		{
			trace("Mouse Out");
			if ( !isBuildComplete() )
			{
				State_GameLoop.getInstance().showBuildCursor(false);
			}
		}
		
		protected function onMouseDown( event:MouseEvent ):void
		{
		}
		
		protected function onMouseUp( event:MouseEvent ):void
		{
			m_buildTick = 0;
			build();
		}
		
		public function buildReady():void
		{
			
			m_build = true;
			registerEvents();
		}
		
		public function buildFinished():void
		{
			m_build = false;
			unregisterEvents();
			onBuildComplete();
		}
		
		public function onBuildComplete():void
	    {
			m_sprite.alpha = 1;
			
		}
		
		public function isBuildComplete():Boolean
		{
			return (m_buildProgress >= m_buildMax);
		}
		
		override public function animationHit():void 
		{
			// do nothing
		}
		
		override public function addKillCount():void
		{
			// do nothing
		}

	}//end CTeeloBaseBuilding

}