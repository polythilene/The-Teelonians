///////////////////////////////////////////////////////////
//  CBaseLinearMissile.as
//  Macromedia ActionScript Implementation of the Class CBaseLinearMissile
//  Generated by Enterprise Architect
//  Created on:      14-Dec-2010 2:03:53 PM
//  Original author: Kurniawan Fitriadi
///////////////////////////////////////////////////////////

package com.game
{
	import com.game.CBaseMissile;
	import com.game.CBaseTeelos;
	import flash.display.DisplayObjectContainer;

	/**
	 * @author Kurniawan Fitriadi
	 * @version 1.0
	 * @created 14-Dec-2010 2:03:53 PM
	 */
	public class CBaseLinearMissile extends CBaseMissile
	{
		protected var m_targetTimes:int;
		
		public function CBaseLinearMissile(){}
		
		override public function move(elapsedTime:int):void
		{
			m_sprite.x += (elapsedTime * m_speed) * m_direction;
		}
		
		override public function onCreate(container:DisplayObjectContainer, x:int, y:int, direction:int, faction:int, lane:int, damage:int, counterOf:int, counterDamage:int, level:int, detectInvisible:Boolean, owner:CBaseTeelos, exParams:Object = null):void 
		{
			super.onCreate(container, x, y, direction, faction, lane, damage, counterOf, counterDamage, level, detectInvisible, owner, exParams);
			m_targetTimes = 1;	// how many targets until missile destroyed?
		}
		
		override public function update(elapsedTime:int):void 
		{
			super.update(elapsedTime);
			
			if ( m_sprite.x < -300 || m_sprite.x > 1200 )
			{
				m_active = false;
			}
			
			if ( m_active )
			{
				var antiFaction:String = (m_faction == FACTION.PLAYER) ? NPCManager.FACTION_ENEMY : NPCManager.FACTION_PLAYER;
				
				var list:Array = NPCManager.getInstance().getListOfUnit(m_lane, antiFaction);
				if( list.length > 0 )
				{
					for( var i:int = 0; i < list.length; i++ )
					{
						var teelo:CBaseTeelos = CBaseTeelos( list[i] );
						
						if ( !teelo.isDead() && teelo.isStealthUnit() == m_detectInvisible && 
							 m_sprite.hitTestObject(teelo.getSprite()) && !(teelo is IInvulnerable) &&
							 !(teelo is IJumper && !IJumper(teelo).hasLanded()) )
						{
							onHit(teelo);
							
							if ( !teelo.isDead() )
							{
								teelo.animationHit();	
							}
							
							if( m_targetTimes <= 0 )
								m_active = false;
						}
					}
				}
			}
		}

	}//end CBaseLinearMissile

}