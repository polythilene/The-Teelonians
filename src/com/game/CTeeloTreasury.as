///////////////////////////////////////////////////////////
//  CTeeloTreasury.as
//  Macromedia ActionScript Implementation of the Class CTeeloTreasury
//  Generated by Enterprise Architect
//  Created on:      22-Dec-2010 2:21:45 PM
//  Original author: poof!
///////////////////////////////////////////////////////////

package com.game
{
	import com.game.ai.AIState_Player_Treasury_Die;
	import com.game.ai.AIState_Player_Treasury_Enter;
	import com.game.CTeeloBaseBuilding;
	import flash.display.DisplayObjectContainer;
	import math.OpMath;

	/**
	 * @author poof!
	 * @version 1.0
	 * @created 22-Dec-2010 2:21:45 PM
	 */
	public class CTeeloTreasury extends CTeeloBaseBuilding
	{
		private var m_generateTimeCtr:int;
		
		public function CTeeloTreasury()
		{
			super.initialize();
			
			m_attack = 0;
			m_defense = 0;
			m_maxLife = 20;
			m_unitClass = UNITCLASS.BUILDING;
			m_counterClass = UNITCLASS.NONE;
			m_counterBonus = 0;
			m_baseSpeed = 0.05;
			m_baseCooldownTime = 2000;
			m_stealthUnit = false;
			m_trainCost = 150;
		}
		
		override protected function createSprite():void 
		{
			super.createSprite();
			m_sprite = new mcTeelo_Treasury();
		}
		
		override public function onCreate(lane:int, targetPosX:int, container:DisplayObjectContainer):void 
		{
			super.onCreate(lane, targetPosX, container);
			m_generateTimeCtr = 0;
			changeAIState( AIState_Player_Treasury_Enter.getInstance() );
		}
		
		override public function update(elapsedTime:int):void 
		{
			super.update(elapsedTime);
			
			if( m_active && !m_dead && isBuildComplete() )
			{
				m_generateTimeCtr += elapsedTime;
				if ( m_generateTimeCtr > 8000 )
				{
					var offsetX:int = x + OpMath.randomRange( -30, 30 );
					var offsetY:int = y - OpMath.randomRange( 50, 100 );
					ItemManager.getInstance().add( CItem_GoldCoin, m_container.stage, offsetX, offsetY, y );
					m_generateTimeCtr = 0;
				}
			}
		}
		
		override public function attack(target:CBaseTeelos):void 
		{
			// do nothing
		}
		
		override public function damage(value:int, source:CBaseTeelos):void 
		{
			if ( value > 0 )
			{
				var def:int = Math.ceil( defense / 2 );
				var dmg:int = (def > value) ? 1 : value - def;
			}
			else
			{
				dmg = value;
			}
			
			if( dmg == 0 ) 
				dmg = 1;
				
			m_currLife -= dmg;
			if( m_currLife < 0 )
			{
				m_dead = true;
				changeAIState( AIState_Player_Treasury_Die.getInstance() );
			}
			
			m_currLife = Math.min(m_currLife, m_maxLife);		// cap value when unit is healed
			
			// tint sprite
			var col:int = (value > 0) ? 0xFF0000 : 0x00FF00;
			tintOnHit(col);
		}
	}//end CTeeloTreasury

}