///////////////////////////////////////////////////////////
//  CBaseParabolicMissile.as
//  Macromedia ActionScript Implementation of the Class CBaseParabolicMissile
//  Generated by Enterprise Architect
//  Created on:      16-Des-2010 0:11:07
//  Original author: Kurniawan Fitriadi
///////////////////////////////////////////////////////////

package com.game
{
	import com.game.ai.AIState_Enemy_Hit;
	import com.game.ai.AIState_Player_Hit;
	import com.game.CBaseMissile;
	import flash.display.DisplayObjectContainer;
	import com.greensock.easing.*;
	import com.greensock.TweenMax;
	import math.OpMath;
	
	/**
	 * @author Kurniawan Fitriadi
	 * @version 1.0
	 * @created 16-Des-2010 0:11:07
	 */
	public class CBaseParabolicMissile extends CBaseMissile
	{
		protected var m_groundLevel:int;
		
		
		
		public function CBaseParabolicMissile(){}
		
		override public function onCreate(container:DisplayObjectContainer, x:int, y:int, 
										  direction:int, faction:int, lane:int, damage:int, 
										  counterOf:int, counterDamage:int, level:int, 
										  detectInvisible:Boolean, owner:CBaseTeelos, exParams:Object = null):void 
		{
			super.onCreate(container, x, y, direction, faction, lane, damage, counterOf, counterDamage, level, detectInvisible, owner, exParams);
			
			m_active = ( exParams != null );
			if ( m_active )
			{
				var target:CBaseTeelos = exParams["target"];
				m_groundLevel = exParams["groundLevel"];
				
				var dist:Number = OpMath.distance2(m_owner.x, m_owner.y, target.x, target.y);
				var midX:int = ((target.x - m_sprite.x) / 2) * direction;
				var midY:int = m_groundLevel - (200 + (dist * 0.03));
				var time:Number = dist * 0.0020;
				TweenMax.to( m_sprite, time, { bezier:[ { x:owner.x + midX, y:midY }, { x:target.x, y:target.y - 10 } ], orientToBezier:true, 
							 ease:Linear.easeNone,onComplete:function():void { onGroundLevel(); } } );
			}
		}
		
		override public function onRemove():void 
		{
			TweenMax.killTweensOf(m_sprite);
			super.onRemove();
		}
		
		override public function update(elapsedTime:int):void 
		{
			super.update(elapsedTime);
			
			if ( m_active && m_sprite.y > m_groundLevel )
			{
				m_active = false;
			}
			
			if ( m_active )
			{
				var antiFaction:String = (m_faction == FACTION.PLAYER) ? NPCManager.FACTION_ENEMY : NPCManager.FACTION_PLAYER;
				
				var list:Array = NPCManager.getInstance().getListOfUnit(m_lane, antiFaction);
				if( list.length > 0 )
				{
					for( var i:int = 0; i < list.length; i++ )
					{
						var teelo:CBaseTeelos = CBaseTeelos( list[i] );
						
						if ( !teelo.isDead() && teelo.isStealthUnit() == m_detectInvisible && 
							  m_sprite.hitTestObject(teelo.getSprite()) )
						{
							onHit(teelo);
							
							if ( !teelo.isDead() )
							{
								teelo.animationHit();
							}
							
							m_active = false;
						}
					}
				}
			}
		}
		
		protected function onGroundLevel():void
		{
			m_active = false;
		}
		

	}//end CBaseParabolicMissile

}