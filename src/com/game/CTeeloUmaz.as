///////////////////////////////////////////////////////////
//  CTeeloUmaz.as
//  Macromedia ActionScript Implementation of the Class CTeeloUmaz
//  Generated by Enterprise Architect
//  Created on:      23-Dec-2010 11:25:02 AM
//  Original author: poof!
///////////////////////////////////////////////////////////

package com.game
{
	import com.game.ai.AIState_Jumper_Run;
	import com.game.CEnemyTeelos;
	import flash.display.LineScaleMode;
	import flash.display.DisplayObjectContainer;
	import com.greensock.TweenMax;
	import math.OpMath;

	/**
	 * @author poof!
	 * @version 1.0
	 * @created 23-Dec-2010 11:25:02 AM
	 */
	public class CTeeloUmaz extends CEnemyTeelos implements IJumper
	{
		private var m_jumped:Boolean;
		private var m_hasLanded:Boolean;
		
		public function CTeeloUmaz(){

		}
		
		override public function initialize():void 
		{
			super.initialize();
			
			m_attack = 10;
			m_defense = 2;
			m_maxLife = 18;
			m_unitClass = UNITCLASS.INFANTRY;
			m_counterClass = UNITCLASS.MAGE;
			m_counterBonus = 3;
			m_baseSpeed = 0.07;
			m_dropMin = 0;
			m_dropMax = 3;
			m_baseCooldownTime = 1200;
		}
		
		override protected function createSprite():void 
		{
			super.createSprite();
			m_sprite = new mcTeelo_Umaz();
		}
		
		override public function onCreate(lane:int, targetPosX:int, container:DisplayObjectContainer):void 
		{
			super.onCreate(lane, targetPosX, container);
			m_attackRange = 10;
			m_attackRange = 50;
			m_jumped = false;
			m_hasLanded = true;
			changeAIState( AIState_Jumper_Run.getInstance() );
		}
		
		public function doJump():void
		{
			m_attackRange = 40;
			
			var myPosX:int = m_sprite.x;
			var myPosY:int = m_sprite.y;
			
			var targetX:int = m_sprite.x - 100;
			var targetY:int = m_sprite.y;
			
			var dist:Number = OpMath.distance2(myPosX, myPosY, targetX, targetY);
			var midX:int = (myPosX - targetX) / 2;
			var midY:int = myPosY - 100;
			var time:Number = dist * 0.0075;
			
			m_hasLanded = false;
				
			TweenMax.to( m_sprite, time, { bezier:[ { x:myPosX - midX, y:midY }, { x:targetX, y:targetY } ], 
														orientToBezier:false, 
														onComplete:function():void 
														{
															m_jumped = true;
															m_hasLanded = true;
														} 
													});
		}
		
		override public function animationHit():void 
		{
			if ( m_jumped )
				 super.animationHit();
		}
		
		public function hasLanded():Boolean
		{
			return m_hasLanded;
		}
		
	}//end CTeeloUmaz

}